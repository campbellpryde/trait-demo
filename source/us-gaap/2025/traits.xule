/**
DQC Rules
 (c) Copyright 2017 - 2024, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP ALL

**/

output-attribute file-location
output-attribute file-content
output-attribute file-append

/** CONSTANTS **/ 

constant $CONCEPT = "us-gaap:CashAndCashEquivalentsAtCarryingValue"

constant $TRAIT = "us-gaap-metaModel:LossMember"

constant $TRAIT_DOMAIN =  navigate 'http://www.xbrl.org/2021/arcrole/trait-domain' children taxonomy $META returns target

constant $TRAIT_VALUES = list(for $domain in $TRAIT_DOMAIN
                            $traits = navigate domain-member descendants from $domain taxonomy $META returns target
                            $traits)

output concepts_with_trait

$META.concepts-by-trait(list($TRAIT.to-qname))


output traits_of_concept

list(for $trait in ($META.concept($CONCEPT.to-qname).traits)
                list($trait.name,
                for $label in $trait.all-labels
                list($label.role.uri.split('\/').last, $label.text)
                )
                )

output list_traits

sum($TRAIT_VALUES)

output list_monetary_concepts

filter $META.concepts where $item.data-type.name == xbrli:monetaryItemType returns $item.name

file-location '../concepts.csv'
file-content $rule-value.to-list.to-csv


output inflows_to_stock

navigate 'http://www.xbrl.org/2021/arcrole/instant-inflow' descendants from $CONCEPT.to-qname taxonomy $META returns set(target-name)


output outflows_from_stock

navigate 'http://www.xbrl.org/2021/arcrole/instant-outflow' descendants from $CONCEPT.to-qname taxonomy $META returns set(target-name)


output agg_other

/** Example us-gaap:LiabilitiesNoncurrent **/

navigate 'http://www.xbrl.org/2021/arcrole/aggregate-other' descendants from $CONCEPT.to-qname taxonomy $META returns list (target-name)

output instant_contra

navigate 'http://www.xbrl.org/2021/arcrole/instant-contra' children from $CONCEPT.to-qname taxonomy $META returns list (target-name)


output instant_accrual

navigate 'http://www.xbrl.org/2021/arcrole/instant-accrual' children from $CONCEPT.to-qname taxonomy $META returns list (target-name)